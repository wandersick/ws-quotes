:: Script:      quotes_en.bat
:: Version:     3.0 - 20200312
:: Platform:    Windows
:: Databases:   quotes_en_number, quotes_en.ini
:: Creator:     wandersick - https://wandersick.blogspot.com/2016/08/auto-posting-inspirational-quotes-on.html
:: Purpose:     Publish quotes as tweets to Twitter using PowerShell (Windows)
::
:: Usage:       Save this script and quotes in the same folder.
::              Ensure write access is permitted to quotes_en_number (generated by script on first run)
::
::              Quotes formatted as below should be saved in a text file named quotes_en.ini. Run/schedule this script on Windows.
::
::              “Hold faithfulness and sincerity as first principles.”=“Confucius”
::              “Peace cannot be kept by force. It can only be achieved by understanding.”=“Albert Einstein”
::
:: Caution:     Be careful of chars like " (not “ or ”) in quotes that could break this script

@echo off
:: go to working dir
cd /d %~p0
if not exist quotes_en_number echo 0 >quotes_en_number
for /f "usebackq" %%i in (`type quotes_en_number`) do set skip=%%i&set skipNum=%%i

if %skip% EQU 0 (
  set skip=
) else (
  set skip=skip=%skip%
)
for /f "%skip% usebackq tokens=1,2 delims==" %%i in (`type "quotes_en.ini"`) do (
	powershell -c "c:\ws-quotes\send-mail-en.ps1" -body1 "%%i" -body2 "%%j"
	goto :skip
)
:skip
set /a skipNum+=1
echo %skipNum% > quotes_en_number